group = 'io.github.yeagy'
version = '0.3.0'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

apply plugin: 'java'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'

sourceCompatibility = '1.8'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testCompile 'com.h2database:h2:1.4.189'
    integrationTestRuntime 'org.postgresql:postgresql:9.4-1206-jdbc42'
    integrationTestRuntime 'mysql:mysql-connector-java:5.1.38'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

modifyPom {
    project {
        name 'Better SQL Support'
        description 'Lightweight JDBC enhancement library. Joinless ORM.'
        url 'https://github.com/yeagy/bss'

        scm {
            url 'https://github.com/yeagy/bss'
            connection 'scm:https://github.com/yeagy/bss.git'
            developerConnection 'scm:git://github.com/yeagy/bss.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://opensource.org/licenses/mit-license.php'
            }
        }

        developers {
            developer {
                name 'Chris Yeagy'
                email 'cyeagy@gmail.com'
            }
        }
    }
}